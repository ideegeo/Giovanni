#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;
use Giovanni;
use Getopt::Long;
use Pod::Usage;
use Config::GitLike;

my $help;
my $man;
my $conf  = '.giovanni.conf';
my $debug = 0;
my $offset;

GetOptions(
    "offset|o=s" => \$offset,
    "config|c=s" => \$conf,
    "help|h"     => \$help,
    "debug|d"    => \$debug,
    "man"        => \$man,
) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

my $command = pop(@ARGV);
pod2usage(2) unless $command;

my $config = Config::GitLike->load_file($conf);
my $cnf;
foreach my $line (keys %{$config}) {
    my (undef, $name, $key) = split(/\./, $line);
    $cnf->{$name}->{$key} = $config->{$line};
}

foreach my $project (keys %{$cnf}) {
    $cnf->{$project}->{project} = $project;
    $cnf->{$project}->{command} = $command;
    my $gio = Giovanni->new(debug => $debug, config => $cnf->{$project});

    given ($command) {
        when ('deploy') {
            print "Running deploy for '$project'\n";
            $gio->deploy;
        }
        when ('rollback') {
            print "Running rollback for '$project'\n";
            $gio->rollback($offset);
        }
        default { print "Could not find command '$command'\n"; }
    }
}

=head1 AUTHOR

Lenz Gschwendtner, C<< <lenz@springtimesoft.com> >>

=head1 SYNOPSIS

gio [options] [deploy|rollback|restart]

 Options:
   --config|c        config file to use
   --offset|o        offset to start from in stage list
   --help|h          brief help message
   --debug|d         turn on debug output
   --man             full documentation

=head1 CONFIG

To configure a project to use Giovanni simply put a `.giovanni.conf`
file in the root directory of your program. The syntax of the config
looks like this:


    [deploy "project_name"]
        hosts  = host.name, IP, ...
        root   = /base/path/to/project
        init   = /etc/init.d/project_name
        stages = update_cache, rollout_timestamped, restart_phased, notify
        repo   = git://github.com/username/project_name.git
        cache  = /tmp/repo
        user   = deploy

=head1 BUGS

Please report any bugs or feature requests to C<< <lenz@springtimesoft.com> >>

=head1 SUPPORT

You can find documentation for this module with the man command.

    man gio

=head1 ACKNOWLEDGEMENTS

Thanks for capistrano for showing me how to deploy nicely

=head1 COPYRIGHT & LICENSE

Copyright 2011 Lenz Gschwendtner, springtimesoft LTD, all rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut
